import os
import glob
import pandas as pd
from snakemake.utils import min_version

min_version("5.26.1")

#container: "continuumio/miniconda3:4.9.1"

configfile: "../config/config.yaml"

SAMPLES = pd.read_table(config['samples'])['Sample'].tolist()
CHROMOSOMES = pd.read_table(config['chromosomes'], header=None).iloc[:,0].tolist()

RAW_READ_DIR = config['paths']['raw_reads']
TRIMMED_READ_DIR = config['paths']['trimmed_reads']
TMPDIR = config['paths']['temp_dir']

REFERENCE_GENOME = config['files']['reference_genome']

RAW_READ_FASTQC_DIR = '../results/qc/fastqc_raw_reads'
TRIMMED_READ_FASTQC_DIR = '../results/qc/fastqc_trimmed_reads'

REPRESENTATIVE_SAMPLE = config['variant_calling']['sample']
NUM_REGIONS_PER_CHROM = config['variant_calling']['num_regions_per_chrom']

wildcard_constraints:
    chrom='|'.join([x for x in CHROMOSOMES])

include: "rules/common.smk"
    
rule all:
    input:
        # QC of raw reads
        fastqc_target_files(),
        # Trim reads
        expand('{trimmed_read_dir}/{sample}/{sample}_{read}.fq.gz', trimmed_read_dir=TRIMMED_READ_DIR, sample=SAMPLES, read=['trimmed_1', 'trimmed_2', 'unpaired']),
        expand('../results/fastp_trim_reports/{sample}_fastp.html', sample=SAMPLES),
        # QC of trimmed reads
        expand(['{0}/{{sample}}_trimmed_1{{ext}}'.format(TRIMMED_READ_FASTQC_DIR), 
                '{0}/{{sample}}_trimmed_2{{ext}}'.format(TRIMMED_READ_FASTQC_DIR)],
                sample=SAMPLES, ext=['_fastqc.html', '_fastqc.zip']),
        # Mapping reads
        expand('../results/bams/final/{sample}_merged_sorted_dupsMarked.{ext}', sample=SAMPLES, ext=['bam', 'bam.bai']),
        # Variant calling
        '../resources/wholeGenome_forFreebayes.regions',
        '../resources/freebayes_bams.list',
        '../results/vcf/wholeGenome_allSamples.vcf'

rule create_tmp_dir:
    output: directory(TMPDIR)
    shell: "mkdir {output}"

include: "rules/qc_raw_reads.smk"
include: "rules/trimming.smk"
include: "rules/qc_trimmed_reads.smk"
include: "rules/mapping.smk"
include: "rules/variant_calling.smk"

rule clean:
    params:
        "logs ../results {0}/* {1} fastp.json".format(TRIMMED_READ_DIR, TMPDIR)
    shell:
        "rm -rfv {params}"
