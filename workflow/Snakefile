import os
import glob
import pandas as pd
from snakemake.utils import min_version

min_version('5.26.1')

#container: 'continuumio/miniconda3:4.9.1'

configfile: '../config/hpcnode.yaml'

SAMPLES = pd.read_table(config['samples'])['Sample'].tolist()
CHROMOSOMES = pd.read_table(config['chromosomes'], header=None).iloc[:,0].tolist()

TMPDIR = config['paths']['temp_dir']
RAW_READ_DIR = '{0}/raw_data'.format(config['data_prefix'])
TRIMMED_READ_DIR = '{0}/trimmed_reads'.format(config['data_prefix'])
QC_DIR = '{0}/qc'.format(config['results_prefix'])
BAM_DIR = '{0}/bam'.format(config['results_prefix'])
VARIANT_DIR = '{0}/variants'.format(config['results_prefix'])
PROGRAM_RESOURCE_DIR = '{0}/program_resources'.format(config['results_prefix'])
POP_STRUC_DIR = '{0}/population_structure'.format(config['results_prefix'])

REFERENCE_GENOME = config['files']['reference_genome']

REPRESENTATIVE_SAMPLE = config['variant_calling']['sample']
NODES_PER_CHROM = config['variant_calling']['nodes_per_chrom']
CORES_PER_NODE = config['variant_calling']['cores_per_node']
NODES = ['node{0:0=2d}'.format(x + 1) for x in range(NODES_PER_CHROM)]
if NODES_PER_CHROM == 1:
    NUM_REGIONS_PER_CHROM = CORES_PER_NODE
elif NODES_PER_CHROM > 1:
    NUM_REGIONS_PER_CHROM = NODES_PER_CHROM * CORES_PER_NODE

wildcard_constraints:
    chrom='|'.join([x for x in CHROMOSOMES])

localrules: create_tmp_dir, create_bam_list, region_files_forFreebayes, variant_calling_done, create_pos_file_for_ngsLD

include: 'rules/common.smk'

raw_read_dict = create_raw_read_dict(RAW_READ_DIR, SAMPLES)

rule all:
   input:
        # Trimming, mapping, and QC
        '{0}/multiqc/multiqc_report.html'.format(QC_DIR), 
        # Variant calling
        '{0}/variant_calling.done'.format(VARIANT_DIR),
        # Population structure
        expand('{0}/angsd_gl/full/genolike_allSamples.{{ext}}'.format(POP_STRUC_DIR), ext=['beagle.gz', 'mafs.gz', 'saf.gz', 'saf.idx', 'saf.pos.gz']),
        expand('{0}/angsd_gl/ld/{{chrom}}/{{chrom}}_genolike_allSamples.ld.gz'.format(POP_STRUC_DIR), chrom=CHROMOSOMES)

rule create_tmp_dir:
    output: directory(TMPDIR)
    shell: 'mkdir {output}'

include: 'rules/trimming.smk'
include: 'rules/mapping.smk'
include: 'rules/qc.smk'
include: 'rules/variant_calling.smk'
include: 'rules/population_structure.smk'

rule clean:
    params:
        'logs {0} {1}/* {2} fastp.json'.format(config['results_prefix'], TRIMMED_READ_DIR, TMPDIR)
    shell:
        'rm -rfv {params}'
